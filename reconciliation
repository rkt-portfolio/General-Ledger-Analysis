/*RECONCILIATION*/


/* MERGING THE SUMMARIZED JE AND TB BY ACCOUNT TO GET THE RECONCILIATION */

DATA SASDATA.RECONCILIATION;
MERGE SASDATA.JE_ACCT_SUMM(IN=A) SASDATA.TB_SUMM(IN=B);
BY ACCOUNT;

/* CREATING THE FOUND IN FIELD */

IF A AND B THEN FOUND_IN = "BOTH";
  IF A AND NOT B THEN FOUND_IN = "JE";
  IF NOT A AND B THEN FOUND_IN = "TB";

/* TO SET THE MISSING VALUES TO 0 */

 IF BEG_BAL=. THEN BEG_BAL=0.00;
 IF END_BAL=. THEN END_BAL=0.00;
 IF JE_COUNT=. THEN JE_COUNT=0;
 IF AMOUNT_IN_DOC_CURR = . THEN AMOUNT_IN_DOC_CURR = 0.00;

 JE_ACTIVITY=AMOUNT_IN_DOC_CURR;

/* SETTING THE BEGINING BALANCE FOR IS ACCOUNTS TO 0 */

IF SUBSTR(COMPRESS(ACCOUNT),1,1) IN ('1','2','3','4') THEN DO;
TYPE ="BS";
TB_ACTIVITY=END_BAL-BEG_BAL;
END;
ELSE DO;
TYPE ="IS";
TB_ACTIVITY=END_BAL;
END;

RECON_DIFF=JE_ACTIVITY-TB_ACTIVITY;

FORMAT AMOUNT_IN_DOC_CURR BEG_BAL END_BAL JE_ACTIVITY TB_ACTIVITY RECON_DIFF ABS_DIFF BEST32.;
    ABS_DIFF = ABS(RECON_DIFF);
RUN;

DATA SASDATA.UNRECONCILED;
SET SASDATA.RECONCILIATION;
IF ABS_DIFF GT 1;
RUN;


/* EXPORTING THE RECONCILIATION RESULTS IN AN EXCEL WORKBOOK */

PROC EXPORT DATA= sasdata.RECONCILIATION 
            OUTFILE= "&path.\Output\RECON.xls" 
            DBMS=EXCEL REPLACE;
     SHEET="RECON"; 
RUN;
